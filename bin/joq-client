#!/usr/bin/env perl

use warnings;
use strict;

use Time::HiRes qw(gettimeofday tv_interval);
use Getopt::Long;
use joq;
use joq::file;
use joq::client;
use JSON::XS;

GetOptions(
	'help|?'                => \my $help,
	'debug|dbg|d'           => \my $debug,
	'server|s|host|h=s'     => \my $server,
	'mode|m|format|fmt|f=s' => \my $mode,
);

my $o = {
	debug  => $debug   || 0,
	server => $server  || 'localhost:1970',
	mode   => $mode    || 'text',
};

sub help {
	print <<EOHELP
joq Client v$joq::VERSION

syntax:
$0 [-options] command [args]

options:
 -? -help             display pron picture
 -d -debug            verbose debug output
 -s -server host:port set joq server hostname (default=localhost)
 -m -mode fmt         set output format (yaml,json or text)

commands & args:
status                             : get joq status
load filename                      : load a local file with jobs, 
                                     and send it to the server
rload filename                     : load a remote file with jobs
list                               : list queued jobs 
show jobname|jobid                 : show detail on a given job
add  [shell|code|class] cmd [args] : queue a job
     [name=foo] [delay=0(seconds)] 
     [repeat=0(seconds)] [count=1]
     [\@h:m]
del jobname|jobid                  : unqueue a job, stopping it if it runs
stop jobname|jobid                 : stop a running job
history [jobname|jobid]            : show ended jobs, or detail on one
killall                            : clear the queue, killing all jobs
shutdown                           : send joq server to the graveyard
pause                              : pause job's queue
resume                             : resume job's queue

EOHELP
;
	exit 1;
}

help if $help || !@ARGV;

my $client;
my %commands = (
	load => sub {
		my $file = shift;
		return "file not found" unless $file && -r $file;
		$client->load(parsefile($file));
	},
	rload => sub {
		my $file = shift;
		$client->load($file);
	},
	show => sub {
		my $job = shift;
		return "requires a jobid or a jobname" unless $job;
		$client->show( $job );
	},
	add => sub {
		return "requires at least a shell command" unless @_;
		map { s/^\@/time=/  } @_;
		$client->add( @_ );
	},
	del => sub {
		my $job = shift;
		return "requires a jobid or a jobname" unless $job;
		$client->del( $job );
	},
	stop => sub {
		my $job = shift;
		return "requires a jobid or a jobname" unless $job;
		$client->stop( $job );
	},
	history  => sub { $client->history( shift ) },
	list     => sub { $client->list },
	killall  => sub { $client->killall },
	shutdown => sub { $client->shutdown },
	status   => sub { $client->status },
	pause    => sub { $client->pause },
	resume   => sub { $client->resume },
);

my $cmd = lc(shift @ARGV);
die("i've searched hard, deep and carefully, but i didn't find this command\n")
	unless exists $commands{$cmd};

$client = joq::client->new(%$o);
die("unable to connect\n".$client->error."\n") unless $client->connected;
my $r = $commands{$cmd}(@ARGV);
unless( ref($r) ) {
	print $r,"\n";
} else {
	print( $mode =~ /ya?ml/ ? Dump $r : JSON::XS->new->utf8->pretty->encode($r) , "\n" );
}
0;
